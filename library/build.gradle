apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
/*
 * This Source Code Form is subject to the terms of the Mozilla Public
 * License, v. 2.0. If a copy of the MPL was not distributed with this
 * file, You can obtain one at http://mozilla.org/MPL/2.0/.
 */


android {
    compileSdkVersion rootProject.ext.configuration.compileSdk
    buildToolsVersion rootProject.ext.configuration.buildTools

    lintOptions {
        abortOnError false
    }

    dataBinding {
        enabled true
    }

    defaultConfig {
        minSdkVersion rootProject.ext.configuration.minSdk
        targetSdkVersion rootProject.ext.configuration.targetSdk
        versionCode 1
        versionName "1.0.0"
        vectorDrawables.useSupportLibrary = true
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "com.android.support:appcompat-v7:${rootProject.ext.configuration.supportLibrary}"
    compile "com.android.support:support-v4:${rootProject.ext.configuration.supportLibrary}"
    compile "com.android.support:recyclerview-v7:${rootProject.ext.configuration.supportLibrary}"

    testCompile 'junit:junit:4.12'
    compile 'commons-net:commons-net:3.3'
}

apply from: 'https://raw.githubusercontent.com/Commit451/gradle-android-javadocs/1.0.0/gradle-android-javadocs.gradle'

def myGroupId = 'com.github.josh7up'
def myArtifactId = 'filepicker'
def myVersionNumber = '4.2.0'
def myUsername = 'josh7up'
def myGitUrl = 'https://github.com/josh7up/NoNonsense-FilePicker'

group = myGroupId
version = myVersionNumber
// Important for ensuring this library project is not uploaded to bintray as a sub-project.
archivesBaseName = myArtifactId

install {
    repositories.mavenInstaller {
        pom.project {
            name myArtifactId
            description 'A file/directory-picker for android. Implemented as a library project.'
            url myGitUrl
            inceptionYear '2017'

            packaging 'aar'
            groupId myGroupId
            artifactId myArtifactId
            version myVersionNumber

            licenses {
                license {
                    name 'Mozilla Public License Version 2.0'
                    url 'http://mozilla.org/MPL/2.0'
                    distribution 'repo'
                }
            }
            scm {
                connection "${myGitUrl}.git"
                url myGitUrl

            }
            developers {
                developer {
                    id = myUsername
                    name 'Josh'
                }
            }
        }
    }
}

Properties bintrayProperties = new Properties()
bintrayProperties.load(project.rootProject.file('bintray.properties').newDataInputStream())

bintray {
    user = bintrayProperties.getProperty('user')
    key = bintrayProperties.get('key')
    configurations = ['archives']
    pkg {
        repo = 'android'
        name = myArtifactId
        licenses = ['MPL-2.0']
        vcsUrl = 'https://github.com/bintray/gradle-bintray-plugin.git'
        publish = true
        version {
            name = myVersionNumber
            desc = "${myArtifactId} ${myVersionNumber}"
            released = new Date()
            vcsTag = myVersionNumber
        }
    }
}
