apply plugin: 'com.android.library'
/*
 * This Source Code Form is subject to the terms of the Mozilla Public
 * License, v. 2.0. If a copy of the MPL was not distributed with this
 * file, You can obtain one at http://mozilla.org/MPL/2.0/.
 */


android {
    compileSdkVersion rootProject.ext.configuration.compileSdk
    buildToolsVersion rootProject.ext.configuration.buildTools

    lintOptions {
        abortOnError false
    }

    dataBinding {
        enabled true
    }

    defaultConfig {
        minSdkVersion rootProject.ext.configuration.minSdk
        targetSdkVersion rootProject.ext.configuration.targetSdk
        versionCode 1
        versionName "1.0.0"
        vectorDrawables.useSupportLibrary = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "com.android.support:appcompat-v7:${rootProject.ext.configuration.supportLibrary}"
    implementation "com.android.support:support-v4:${rootProject.ext.configuration.supportLibrary}"
    implementation "com.android.support:recyclerview-v7:${rootProject.ext.configuration.supportLibrary}"

    testImplementation 'junit:junit:4.12'
    implementation 'commons-net:commons-net:3.3'
}

apply from: 'https://raw.githubusercontent.com/josh7up/AndroidMavenBintray/1.0.0/gradle-maven-bintray.gradle'

def groupId = 'com.github.josh7up'
def artifactId = 'filepicker'
def versionNumber = rootProject.ext.gitTag
def gitUrl = 'https://github.com/josh7up/NoNonsense-FilePicker'

configureMavenInstall([
        groupId: groupId,
        artifactId: artifactId,
        versionNumber: versionNumber,
        developerId: 'josh7up',
        gitUrl: gitUrl,
        projectDescription: 'A file/directory-picker for android. Implemented as a library project.',
        projectInceptionYear: 2017,
        licenseName: 'Mozilla Public License Version 2.0',
        licenseUrl: 'http://mozilla.org/MPL/2.0',
        developerName: 'Josh Feldman'
])

Properties bintrayProperties = new Properties()
bintrayProperties.load(project.rootProject.file('bintray.properties').newDataInputStream())

configureBintrayUpload(
        user: bintrayProperties.getProperty('user'),
        key: bintrayProperties.get('key'),
        repoName: 'android',
        artifactId: artifactId,
        licenseId: 'MPL-2.0',
        gitUrl: gitUrl,
        versionNumber: versionNumber
)
